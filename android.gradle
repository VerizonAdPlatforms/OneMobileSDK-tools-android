apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion TARGET_API_VERSION

    aaptOptions {
        failOnMissingConfigEntry true
    }

    lintOptions {
        abortOnError true
        showAll true
        warningsAsErrors true
        explainIssues true

        textReport true
        textOutput "stdout"

        htmlReport false
        xmlReport false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    defaultConfig {
        minSdkVersion MIN_API_VERSION
        targetSdkVersion TARGET_API_VERSION

        versionName LIBRARY_VERSION

        if (project.ext.has('BUILD_CONFIG_CONSTS')) {
            BUILD_CONFIG_CONSTS.each { key, value -> buildConfigField "String", key, "\"${value}\"" }
            manifestPlaceholders = BUILD_CONFIG_CONSTS
        }
        
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    buildTypes {
        debug {
            minifyEnabled MINIFY_LIB
            useProguard MINIFY_LIB
        }
        release {
            minifyEnabled MINIFY_LIB
            useProguard MINIFY_LIB
        }
    }
}
